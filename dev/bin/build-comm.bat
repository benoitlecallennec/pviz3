@ECHO off

IF "%PVIZ3DEV_HOME%" == "" (
	ECHO PVIZ3DEV_HOME environment variable is not defined correctly. 
	GOTO :ERROR
)

IF "%PVIZ3DEV_WORKSPACE%" == "" (
	ECHO PVIZ3DEV_WORKSPACE environment variable is not defined correctly. 
	GOTO :ERROR
)

IF "%PVIZ3_SRC%" == "" (
	ECHO PVIZ3DEV_SRCDIR environment variable is not defined correctly. 
	GOTO :ERROR
)

REM ========================================
REM Default environmental values
REM ========================================
SET PVIZ3DEV_ARCH=x64
SET PVIZ3DEV_TYPE=Release
SET PVIZ3DEV_QT_VER=4.7.4
SET PVIZ3DEV_VTK_VER=5.8.0
SET PVIZ3DEV_BOOST_VER=1.47.0
SET PVIZ3DEV_DRYRUN=0
SET PVIZ3DEV_GTYPE=nmake

IF "%~1" == "" GOTO MAIN
:GETOPTS
IF /I "%~1" == "-h" GOTO HELP
IF /I "%~1" == "-d" SET PVIZ3DEV_DRYRUN=1
IF /I "%~1" == "-a" SET PVIZ3DEV_ARCH=%~2& SHIFT
IF /I "%~1" == "-t" SET PVIZ3DEV_TYPE=%~2& SHIFT
IF /I "%~1" == "-qt" SET PVIZ3DEV_QT_VER=%~2& SHIFT
IF /I "%~1" == "-vtk" SET PVIZ3DEV_VTK_VER=%~2& SHIFT
IF /I "%~1" == "-boost" SET PVIZ3DEV_BOOST_VER=%~2& SHIFT
IF /I "%~1" == "-g" SET PVIZ3DEV_GTYPE=%2 & SHIFT
SHIFT
IF NOT "%~1" == "" GOTO GETOPTS

:MAIN
SET PVIZ3DEV_QT=Qt-%PVIZ3DEV_QT_VER%-%PVIZ3DEV_ARCH%
SET PVIZ3DEV_QT_DIR=%PVIZ3DEV_HOME%\lib\%PVIZ3DEV_QT%
SET PVIZ3DEV_QT_WORKDIR=%PVIZ3DEV_WORKSPACE%\Qt
SET PVIZ3DEV_QT_SRC_DIR=%PVIZ3DEV_QT_WORKDIR%\%PVIZ3DEV_QT%.src
SET PVIZ3DEV_QT_BUILD_DIR=%PVIZ3DEV_QT_WORKDIR%\%PVIZ3DEV_QT%.build

SET PVIZ3DEV_VTK=VTK-%PVIZ3DEV_VTK_VER%-%PVIZ3DEV_ARCH%-%PVIZ3DEV_TYPE%
SET PVIZ3DEV_VTK_DIR=%PVIZ3DEV_HOME%\lib\%PVIZ3DEV_VTK%
SET PVIZ3DEV_VTK_WORKDIR=%PVIZ3DEV_WORKSPACE%\VTK
SET PVIZ3DEV_VTK_SRC_DIR=%PVIZ3DEV_VTK_WORKDIR%\%PVIZ3DEV_VTK%.src
SET PVIZ3DEV_VTK_BUILD_DIR=%PVIZ3DEV_VTK_WORKDIR%\%PVIZ3DEV_VTK%.build

SET PVIZ3DEV_BOOST=Boost-%PVIZ3DEV_BOOST_VER%-%PVIZ3DEV_ARCH%
SET PVIZ3DEV_BOOST_DIR=%PVIZ3DEV_HOME%\lib\%PVIZ3DEV_BOOST%
SET PVIZ3DEV_BOOST_WORKDIR=%PVIZ3DEV_WORKSPACE%\Boost
SET PVIZ3DEV_BOOST_SRC_DIR=%PVIZ3DEV_BOOST_WORKDIR%\%PVIZ3DEV_BOOST%.src
SET PVIZ3DEV_BOOST_BUILD_DIR=%PVIZ3DEV_BOOST_WORKDIR%\%PVIZ3DEV_BOOST%.build

if %PVIZ3DEV_DRYRUN% == 1 (
ECHO ========================================
ECHO PVIZ3DEV_QT=%PVIZ3DEV_QT%
ECHO PVIZ3DEV_QT_DIR=%PVIZ3DEV_QT_DIR%
ECHO PVIZ3DEV_QT_WORKDIR=%PVIZ3DEV_QT_WORKDIR%
ECHO.
ECHO PVIZ3DEV_VTK=%PVIZ3DEV_VTK%
ECHO PVIZ3DEV_VTK_DIR=%PVIZ3DEV_VTK_DIR%
ECHO PVIZ3DEV_VTK_WORKDIR=%PVIZ3DEV_VTK_WORKDIR%
ECHO.
ECHO PVIZ3DEV_BOOST=%PVIZ3DEV_BOOST%
ECHO PVIZ3DEV_BOOST_DIR=%PVIZ3DEV_BOOST_DIR%
ECHO PVIZ3DEV_BOOST_WORKDIR=%PVIZ3DEV_BOOST_WORKDIR%
ECHO ========================================
GOTO :ERROR
)
EXIT /b 0

:HELP
ECHO USAGE : COMMAND [OPTIONS]
ECHO OPTIONS
ECHO     -d     : dry run
ECHO     -a     : architecture (x64 or x86)
ECHO     -t     : build type (Release or Debug)
ECHO     -qt    : Qt version
ECHO     -vtk   : VTK version
ECHO     -boost : BOOST version
ECHO.
EXIT /b 1

:ERROR
EXIT /b 1